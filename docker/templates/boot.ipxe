#!ipxe

dhcp
set keep-san 1
set ipxe_server_ip ${proxydhcp/dhcp-server}

# menu-timeout en menu-default zou je kunnen zetten in http://192.168.2.17/mac/${mac:hexhyp}/ipxe en dan hier naar terug komen?
# zie https://www.redpill-linpro.com/sysadvent/2017/12/13/ipxe-provisioning.html

:start
menu iPXE boot menu for ${initiator-iqn}
item --gap --             ------------------------- Operating systems ------------------------------
# manjaro te groot voor ramdisk (hele iso) en krijg het niet voor elkaar met mount
#item --key m manjaro                  (m) Manjaro KDE 20.1  (64) (from samba) (not working)
item --key u ubuntu                   (u) Boot Ubuntu 20.04 (64) (from samba)
item --key p ubuntu2                  (p) Boot Ubuntu 20.04 (64) (from nfs)
item --key g gparted                  (g) Gparted Live      (from http)
item --key c centos                   (c) CentOS 7          (64) (from nfs)
item --key s slax64                   (s) Slax 9.11 64bit   (from iso http)
item --key t slax32                   (t) Slax 9.11 32bit   (from iso http)
item --gap --             --------------------------------------------------------------------------
#item --key w win10_repair_only        (w) Boot Windows 10 ----- repair mode ------ (from samba)
#item --key 1 winpe_win10              (1) Boot Windows 10 ----- installer .iso --- (from samba)
#item --gap --             --------------------------------------------------------------------------
#item --key s salstar                  (s) Boot from internet -- boot.salstar.sk -- (from WAN)
item --gap --             ------------------------- Advanced options -------------------------------
item --key c config       Configure settings
item shell                Drop to iPXE shell
item reboot               Reboot computer
item
item --key x exit         Exit iPXE and continue BIOS boot

set menu-timeout 30
set menu-default gparted

# TODO losse variable voor NFS

set samba_server_ip  {{ SAMBA_IP_ADDRESS }}
set samba_share      {{ SAMBA_SHARE }}
set samba_username   {{ SAMBA_USERNAME }}
set samba_password   {{ SAMBA_PASSWORD }}

choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}


:slax64
  kernel http://${ipxe_server_ip}/boot/slax64/boot/vmlinuz vga=normal load_ramdisk=1 prompt_ramdisk=0 rw printk.time=0 consoleblank=0 slax.flags=perch,automount from=http://${ipxe_server_ip}/iso/slax-64bit-9.11.0.iso
  # werkt niet kernel http://${ipxe_server_ip}/boot/slax64/boot/vmlinuz vga=normal load_ramdisk=1 prompt_ramdisk=0 rw printk.time=0 consoleblank=0 slax.flags=perch,automount root=/dev/nfs netboot=nfs nfsroot=${samba_server_ip}:/volume1/${samba_share}/iso/slax/64 NFSOPTS=vers=3 ip=dhcp
  initrd http://${ipxe_server_ip}/boot/slax64/boot/initrfs.img
  boot
  goto exit

:slax32
  kernel http://${ipxe_server_ip}/boot/slax32/boot/vmlinuz vga=normal load_ramdisk=1 prompt_ramdisk=0 rw printk.time=0 consoleblank=0 slax.flags=perch,automount from=http://${ipxe_server_ip}/iso/slax-32bit-9.11.0.iso
  initrd http://${ipxe_server_ip}/boot/slax32/boot/initrfs.img
  boot
  goto exit

:gparted
  kernel http://${ipxe_server_ip}/boot/gparted/vmlinuz boot=live config components union=overlay username=user noswap noeject ip= vga=788 fetch=http://${ipxe_server_ip}/boot/gparted/filesystem.squashfs
  initrd http://${ipxe_server_ip}/boot/gparted/initrd.img
  boot
  goto exit

:ubuntu
  # ! You put here the login details for your local samba server,
  # which is serviing the mounted ubuntu-desktop.iso files (otherwise it will not boot)
  kernel http://${ipxe_server_ip}/boot/ubuntu/vmlinuz showmounts toram root=/dev/cifs boot=casper netboot=cifs nfsroot=//${samba_server_ip}/${samba_share}/iso/ubuntu/64  NFSOPTS=-ouser=${samba_username},pass=${samba_password} ip=dhcp ro
  initrd http://${ipxe_server_ip}/boot/ubuntu/initrd
  boot
  goto exit

:ubuntu2
  # ! You put here the login details for your local samba server,
  # which is serviing the mounted ubuntu-desktop.iso files (otherwise it will not boot)
  kernel http://${ipxe_server_ip}/boot/ubuntu/vmlinuz showmounts toram boot=casper root=/dev/nfs netboot=nfs nfsroot=${samba_server_ip}:/volume1/${samba_share}/iso/ubuntu/64 NFSOPTS=vers=3 ip=dhcp
  initrd http://${ipxe_server_ip}/boot/ubuntu/initrd
  boot
  goto exit

:centos2
  kernel http://${ipxe_server_ip}/boot/centos/isolinux/vmlinuz0 root=/dev/nfs netboot=nfs nfsroot=${samba_server_ip}:/volume1/${samba_share}/iso/centos/64 NFSOPTS=vers=3 ip=dhcp ipv6.disable=1 
  initrd http://${ipxe_server_ip}/boot/centos/isolinux/initrd0.img
  boot
  goto exit

:centos
  # kernel http://${ipxe_server_ip}/boot/centos/isolinux/vmlinuz inst.repo=nfs:${samba_server_ip}:/volume1/${samba_share}/iso/centos inst.stage2=nfs:${samba_server_ip}:/volume1/${samba_share}/iso/centos  ip=dhcp
  kernel http://${ipxe_server_ip}/boot/centos/isolinux/vmlinuz inst.repo=nfs:${samba_server_ip}:/volume1/${samba_share}/iso/centos  ip=dhcp
  # kernel http://${ipxe_server_ip}/boot/centos/isolinux/vmlinuz0  root=/dev/nfs netboot=nfs nfsroot=${samba_server_ip}:/volume1/${samba_share}/iso/centos/64 NFSOPTS=vers=3 inst.repo=nfs:${samba_server_ip}:/volume1/${samba_share}/iso/centos inst.stage2=nfs:${samba_server_ip}:/volume1/${samba_share}/iso/centos  ip=dhcp
  # kernel http://${ipxe_server_ip}/boot/centos/isolinux/vmlinuz0 inst.repo=ftp://${samba_username}:${samba_password}@${samba_server_ip}/${samba_share}/iso/centos/64 inst.stage2=ftp://${samba_username}:${samba_password}@${samba_server_ip}/${samba_share}/iso/centos/64  ip=dhcp
  initrd http://${ipxe_server_ip}/boot/centos/isolinux/initrd.img
  boot
  goto exit

:manjaro
  kernel http://${ipxe_server_ip}/boot/manjaro/vmlinuz-x86_64 archisobasedir=arch archiso_nfs_srv=${samba_server_ip}:/${samba_share}/iso/manjaro ip=dhcp ro
  #initrd http://${ipxe_server_ip}/boot/manjaro/intel-ucode.img, http://${ipxe_server_ip}/boot/manjaro/amd_ucode.img, http://${ipxe_server_ip}/boot/manjaro/initramfs-x86_64.img
  initrd http://${ipxe_server_ip}/boot/manjaro/initramfs-x86_64.img
  boot
  goto exit

:winpe_win10
  # ! you must get these files from a winpe environment (using WADK on windows)
  # See: http://ipxe.org/howto/winpe for further instructions

  kernel http://${ipxe_server_ip}/ipxe/wimboot
  initrd http://${ipxe_server_ip}/iso/extracted/clean/winpe/boot/BCD         BCD
  initrd http://${ipxe_server_ip}/iso/extracted/clean/winpe/boot/boot.sdi    boot.sdi
  initrd http://${ipxe_server_ip}/iso/extracted/modified/winpe/sources/win10_setup.exe/boot.wim boot.wim
  boot


:win10_repair_only
  # ! This files can be taken directly from the windows installer ISO
  # HOWEVER only the 'Repair' mode button will works without WinPE's boot.wim environment

  kernel http://${ipxe_server_ip}/ipxe/wimboot
  initrd http://${ipxe_server_ip}/iso/extracted/clean/win10/boot/bcd         BCD
  initrd http://${ipxe_server_ip}/iso/extracted/clean/win10/boot/boot.sdi    boot.sdi
  initrd http://${ipxe_server_ip}/iso/extracted/clean/win10/sources/boot.wim boot.wim
  boot


#:salstar
  # Open http://boot.salstar.sk in a web browser for more details
#  chain http://boot.salstar.sk





:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start
